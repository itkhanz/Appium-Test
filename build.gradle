plugins {
    id 'java'
}

group = 'com.itkhanz'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'io.appium:java-client:9.0.0'
    testImplementation 'org.testng:testng:7.8.0'
    testImplementation 'org.assertj:assertj-core:3.24.2'
    implementation 'org.apache.logging.log4j:log4j-core:2.20.0'
    implementation 'org.apache.logging.log4j:log4j-api:2.20.0'
    implementation 'org.apache.logging.log4j:log4j-slf4j2-impl:2.20.0'
    implementation 'org.aeonbits.owner:owner:1.0.12'
}

test {
    useTestNG() {
        //set TestNG output dir
        outputDirectory = file("$projectDir/test-output")

        // To generate reports by TestNG library
        useDefaultListeners = true

        suites "src/test/resources/test-suites/testng.xml"

    }

    //gradle report output
    //The default location for gradle html report $buildDir/reports/tests/test
    reports.html.destination(file("$projectDir/gradle-reports"))

    testLogging {
        //we want display the following test events
        events "passed", "skipped", "failed", "standardOut", "standardError"

        //print the test results summary to console output
        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} passed, ${result.failedTestCount} failed, ${result.skippedTestCount} skipped)"
                def startItem = '|  ', endItem = '  |'
                def repeatLength = startItem.length() + output.length() + endItem.length()
                println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
            }
        }
    }


}